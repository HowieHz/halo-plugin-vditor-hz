plugins {
    id 'java'
    id "com.github.node-gradle.node" version "7.0.2"
    id "io.freefair.lombok" version "8.0.1"
    id "run.halo.plugin.devtools" version "0.2.0"
}

group 'top.mczhengyi'
sourceCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
    maven { url 'https://s01.oss.sonatype.org/content/repositories/releases' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    implementation platform('run.halo.tools.platform:plugin:2.17.0-SNAPSHOT')
    compileOnly 'run.halo.app:api'

    testImplementation 'run.halo.app:api'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

node {
    nodeProjectDir = file("${project.projectDir}/console")
}

tasks.register('downloadDependencies') {
    doFirst {
        if (!System.properties['os.name'].toLowerCase().contains('windows')) {
            // 在非 Windows 系统上添加执行权限
            exec {
                commandLine 'chmod', '+x', 'download_dist.sh'
            }
        }
    }
    doLast {
        if (System.properties['os.name'].toLowerCase().contains('windows')) {
            exec {
                commandLine 'cmd', '/c', 'download_dist.bat'
            }
        } else {
            exec {
                commandLine 'bash', '-c', './download_dist.sh'
            }
        }
    }
}

tasks.register('installDepsForUI', PnpmTask) {
    args = ['install']

   dependsOn('downloadDependencies')
}

tasks.register('buildFrontend', PnpmTask) {
    args = ['build']

    dependsOn('installDepsForUI')
}

build {
    // build frontend before build
    tasks.named('compileJava').configure {
        dependsOn('buildFrontend')
    }
}

halo {
    version = '2.21'
    superAdminUsername = 'admin'
    superAdminPassword = 'admin'
    externalUrl = 'http://localhost:8090'
    debug = true
}